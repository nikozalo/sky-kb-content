// SPDX-License-Identifier: AGPL-3.0
pragma solidity ^0.8.0;

import { Ethereum }  from "lib/grove-address-registry/src/Ethereum.sol";
import { Avalanche } from "lib/grove-address-registry/src/Avalanche.sol";
import { Base }      from "lib/grove-address-registry/src/Base.sol";
import { Plume }     from "lib/grove-address-registry/src/Plume.sol";
import { Robinhood } from "lib/grove-address-registry/src/Robinhood.sol";

import { IERC20 } from "forge-std/interfaces/IERC20.sol";

import { IALMProxy } from "lib/grove-alm-controller/src/interfaces/IALMProxy.sol";

import { IExecutor } from "lib/grove-gov-relay/src/interfaces/IExecutor.sol";

import { ArbitrumForwarder }      from "xchain-helpers/forwarders/ArbitrumForwarder.sol";
import { ArbitrumERC20Forwarder } from "xchain-helpers/forwarders/ArbitrumERC20Forwarder.sol";
import { CCTPv2Forwarder }        from "xchain-helpers/forwarders/CCTPv2Forwarder.sol";
import { OptimismForwarder }      from "xchain-helpers/forwarders/OptimismForwarder.sol";

import { OptionsBuilder } from "lib/xchain-helpers/lib/devtools/packages/oapp-evm/contracts/oapp/libs/OptionsBuilder.sol";

import { GroveLiquidityLayerHelpers } from "../helpers/GroveLiquidityLayerHelpers.sol";
import { GrovePauHelpers }            from "../helpers/GrovePauHelpers.sol";
import { UniswapV3Helpers }           from "../helpers/UniswapV3Helpers.sol";

interface IStarSpellLike {

    /**
     * @notice Executes actions performed on behalf of the `SubProxy` – i.e. the actual payload
     * @dev Required, will be called by the StarGuard during permissionless execution
     */
    function execute() external;

    /**
     * @notice Checks if the star payload is executable in the current block
     * @dev Required, useful for implementing "earliest launch date" or "office hours" strategy
     * @return result The result of the check (true = executable, false = not)
     */
    function isExecutable() external view returns (bool result);

}

/**
 * @dev    Base smart contract for Ethereum.
 * @author Steakhouse Financial
 */
abstract contract GrovePayloadEthereum is IStarSpellLike {

    using OptionsBuilder for bytes;

    // These need to be immutable (delegatecall) and can only be set in constructor
    address public immutable PAYLOAD_AVALANCHE;
    address public immutable PAYLOAD_BASE;
    address public immutable PAYLOAD_PLUME;
    address public immutable PAYLOAD_ROBINHOOD;

    address internal constant ROBINHOOD_DELAYED_INBOX  = 0x1A07cc4BD17E0118BdB54D70990D2158AbAD7a2D; // Robinhood L1 bridge (Delayed Inbox) on Ethereum

    function isExecutable() external view virtual returns (bool result) {
        return true;
    }

    function execute() external {
        _execute();

        if (PAYLOAD_AVALANCHE != address(0)) {
            CCTPv2Forwarder.sendMessage({
                messageTransmitter  : CCTPv2Forwarder.MESSAGE_TRANSMITTER_CIRCLE_ETHEREUM,
                destinationDomainId : CCTPv2Forwarder.DOMAIN_ID_CIRCLE_AVALANCHE,
                recipient           : Avalanche.GROVE_CCTP_V2_RECEIVER,
                messageBody         : _encodePayloadQueue(PAYLOAD_AVALANCHE)
            });
        }

        if (PAYLOAD_BASE != address(0)) {
            OptimismForwarder.sendMessageL1toL2({
                l1CrossDomain : OptimismForwarder.L1_CROSS_DOMAIN_BASE,
                target        : Base.GROVE_RECEIVER,
                message       : _encodePayloadQueue(PAYLOAD_BASE),
                gasLimit      : 1_000_000
            });
        }

        if (PAYLOAD_PLUME != address(0)) {
            ArbitrumERC20Forwarder.sendMessageL1toL2({
                l1CrossDomain : ArbitrumERC20Forwarder.L1_CROSS_DOMAIN_PLUME,
                target        : Plume.GROVE_RECEIVER,
                message       : _encodePayloadQueue(PAYLOAD_PLUME),
                gasLimit      : 1_000_0000,
                maxFeePerGas  : 5_000e9,
                baseFee       : block.basefee
            });
        }

        if (PAYLOAD_ROBINHOOD != address(0)) {
            ArbitrumForwarder.sendMessageL1toL2({
                l1CrossDomain : ROBINHOOD_DELAYED_INBOX,
                target        : Robinhood.GROVE_RECEIVER,
                message       : _encodePayloadQueue(PAYLOAD_ROBINHOOD),
                gasLimit      : 1_000_000,
                maxFeePerGas  : 50e9,
                baseFee       : block.basefee
            });
        }
    }

    function _execute() internal virtual;

    function _encodePayloadQueue(address _payload) internal pure returns (bytes memory) {
        address[] memory targets        = new address[](1);
        uint256[] memory values         = new uint256[](1);
        string[] memory signatures      = new string[](1);
        bytes[] memory calldatas        = new bytes[](1);
        bool[] memory withDelegatecalls = new bool[](1);

        targets[0]           = _payload;
        values[0]            = 0;
        signatures[0]        = 'execute()';
        calldatas[0]         = '';
        withDelegatecalls[0] = true;

        return abi.encodeCall(IExecutor.queue, (
            targets,
            values,
            signatures,
            calldatas,
            withDelegatecalls
        ));
    }

    function _onboardERC4626Vault(address vault, uint256 depositMax, uint256 depositSlope, uint256 shareUnit, uint256 maxAssetsPerShare) internal {
        GroveLiquidityLayerHelpers.onboardERC4626Vault(
            Ethereum.ALM_CONTROLLER,
            Ethereum.ALM_RATE_LIMITS,
            vault,
            depositMax,
            depositSlope,
            shareUnit,
            maxAssetsPerShare
        );
    }

    function _onboardERC7540Vault(address vault, uint256 depositMax, uint256 depositSlope) internal {
        GroveLiquidityLayerHelpers.onboardERC7540Vault(
            Ethereum.ALM_RATE_LIMITS,
            vault,
            depositMax,
            depositSlope
        );
    }

    function _offboardERC7540Vault(address vault) internal {
        GroveLiquidityLayerHelpers.offboardERC7540Vault(
            Ethereum.ALM_RATE_LIMITS,
            vault
        );
    }

    function _transferAssetFromAlmProxy(address asset, address destination, uint256 amount) internal {
        // Grant controller role to Grove Proxy
        IALMProxy(Ethereum.ALM_PROXY).grantRole(
            IALMProxy(Ethereum.ALM_PROXY).CONTROLLER(),
            Ethereum.GROVE_PROXY
        );

        IALMProxy(Ethereum.ALM_PROXY).doCall(
            asset,
            abi.encodeCall(IERC20(asset).transfer, (destination, amount))
        );

        // Revoke controller role from Grove Proxy
        IALMProxy(Ethereum.ALM_PROXY).revokeRole(
            IALMProxy(Ethereum.ALM_PROXY).CONTROLLER(),
            Ethereum.GROVE_PROXY
        );
    }

    function _setUSDSMintRateLimit(uint256 maxAmount, uint256 slope) internal {
        GroveLiquidityLayerHelpers.setUSDSMintRateLimit(
            Ethereum.ALM_RATE_LIMITS,
            maxAmount,
            slope
        );
    }

    function _setUSDSToUSDCRateLimit(uint256 maxAmount, uint256 slope) internal {
        GroveLiquidityLayerHelpers.setUSDSToUSDCRateLimit(
            Ethereum.ALM_RATE_LIMITS,
            maxAmount,
            slope
        );
    }

    function _setCentrifugeCrosschainTransferRateLimit(address centrifugeVault, uint16 destinationCentrifugeId, uint256 maxAmount, uint256 slope) internal {
        GroveLiquidityLayerHelpers.setCentrifugeCrosschainTransferRateLimit(
            Ethereum.ALM_RATE_LIMITS,
            centrifugeVault,
            destinationCentrifugeId,
            maxAmount,
            slope
        );
    }

    function _onboardAaveToken(address token, uint256 maxSlippage, uint256 depositMax, uint256 depositSlope) internal {
        GroveLiquidityLayerHelpers.onboardAaveToken(
            Ethereum.ALM_CONTROLLER,
            Ethereum.ALM_RATE_LIMITS,
            token,
            maxSlippage,
            depositMax,
            depositSlope
        );
    }

    function _onboardCurvePool(
        address pool,
        uint256 maxSlippage,
        uint256 swapMax,
        uint256 swapSlope,
        uint256 depositMax,
        uint256 depositSlope,
        uint256 withdrawMax,
        uint256 withdrawSlope
    ) internal {
        GroveLiquidityLayerHelpers.onboardCurvePool(
            Ethereum.ALM_CONTROLLER,
            Ethereum.ALM_RATE_LIMITS,
            pool,
            maxSlippage,
            swapMax,
            swapSlope,
            depositMax,
            depositSlope,
            withdrawMax,
            withdrawSlope
        );
    }

    function _onboardUniswapV3Pool(
        address pool,
        UniswapV3Helpers.UniswapV3PoolParams  memory poolParams,
        UniswapV3Helpers.UniswapV3TokenParams memory token0Params,
        UniswapV3Helpers.UniswapV3TokenParams memory token1Params
    ) internal {
        GroveLiquidityLayerHelpers.onboardUniswapV3Pool(
            Ethereum.ALM_CONTROLLER,
            Ethereum.ALM_RATE_LIMITS,
            pool,
            poolParams,
            token0Params,
            token1Params
        );
    }

    function _setUsdsMintBurnPauRateLimits(
        address rateLimits,
        uint256 mintMax,
        uint256 mintSlope,
        uint256 burnMax,
        uint256 burnSlope
    ) internal {
        GrovePauHelpers.setUsdsMintBurnRateLimit(
            rateLimits,
            mintMax,
            mintSlope,
            burnMax,
            burnSlope
        );
    }

    function _setPsmSwapPauRateLimits(
        address rateLimits,
        uint256 usdsToUsdcMax,
        uint256 usdsToUsdcSlope,
        uint256 usdcToUsdsMax,
        uint256 usdcToUsdsSlope
    ) internal {
        GrovePauHelpers.setPsmSwapRateLimit(
            rateLimits,
            usdsToUsdcMax,
            usdsToUsdcSlope,
            usdcToUsdsMax,
            usdcToUsdsSlope
        );
    }

    function _setBasinPauRateLimits(
        address rateLimits,
        address basin,
        uint256 depositMax,
        uint256 depositSlope
    ) internal {
        GrovePauHelpers.setBasinRateLimit(
            rateLimits,
            basin,
            Ethereum.USDS,
            Ethereum.USDC,
            depositMax,
            depositSlope
        );
    }

}
