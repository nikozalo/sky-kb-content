// SPDX-License-Identifier: AGPL-3.0
pragma solidity 0.8.25;

import { CCTPv2Forwarder } from "lib/xchain-helpers/src/forwarders/CCTPv2Forwarder.sol";

import { Ethereum }  from "lib/grove-address-registry/src/Ethereum.sol";
import { Avalanche } from "lib/grove-address-registry/src/Avalanche.sol";
import { Base }      from "lib/grove-address-registry/src/Base.sol";
import { Plume }     from "lib/grove-address-registry/src/Plume.sol";

import { MainnetControllerInit, ControllerInstance } from "lib/grove-alm-controller/deploy/MainnetControllerInit.sol";

import { MainnetController } from "lib/grove-alm-controller/src/MainnetController.sol";
import { RateLimitHelpers }  from "lib/grove-alm-controller/src/RateLimitHelpers.sol";

import { IRateLimits } from "lib/grove-alm-controller/src/interfaces/IRateLimits.sol";

import { CastingHelpers }             from "src/libraries/helpers/CastingHelpers.sol";
import { GroveLiquidityLayerHelpers } from "src/libraries/helpers/GroveLiquidityLayerHelpers.sol";

import { GrovePayloadEthereum } from "src/libraries/payloads/GrovePayloadEthereum.sol";

/**
 * @title  January 15, 2026 Grove Ethereum Proposal
 * @author Grove Labs
 */
contract GroveEthereum_20260115 is GrovePayloadEthereum {

    address internal constant NEW_CONTROLLER = 0xfd9dEA9a8D5B955649579Af482DB7198A392A9F5;

    // BEFORE :          0 max ;          0/day slope
    // AFTER  : 50,000,000 max ; 50,000,000/day slope
    uint256 internal constant CCTP_RATE_LIMIT_MAX   = 50_000_000e6;
    uint256 internal constant CCTP_RATE_LIMIT_SLOPE = 50_000_000e6 / uint256(1 days);

    constructor() {
        PAYLOAD_BASE = 0xAe9EAd94B00d137f01159A7F279c0b78dd04c860;
    }

    function _execute() internal override {
        // [Mainnet] Upgrade MainnetController to v1.8.0
        //   Forum : https://forum.sky.money/t/january-15th-2026-proposed-changes-to-grove-for-upcoming-spell/27570#p-105288-h-3-mainnet-upgrade-mainnetcontroller-to-v180-14
        _upgradeController();

        // [Base] Onboard Grove Liquidity Layer and CCTP for Base
        //   Forum : https://forum.sky.money/t/january-15th-2026-proposed-changes-to-grove-for-upcoming-spell/27570#p-105288-h-1-base-onboard-grove-liquidity-layer-and-cctp-for-base-2
        _onboardCctpTransfersToBase();
    }

    function _upgradeController() internal {
        address[] memory relayers = new address[](1);
        relayers[0] = Ethereum.ALM_RELAYER;

        MainnetControllerInit.MintRecipient[] memory mintRecipients = new MainnetControllerInit.MintRecipient[](1);
        // Note: Re-setting the Avalanche CCTP mint recipient set in the GroveEthereum_20250821 proposal
        mintRecipients[0] = MainnetControllerInit.MintRecipient({
            domain        : CCTPv2Forwarder.DOMAIN_ID_CIRCLE_AVALANCHE,
            mintRecipient : CastingHelpers.addressToCctpRecipient(Avalanche.ALM_PROXY)
        });

        MainnetControllerInit.CentrifugeRecipient[] memory centrifugeRecipients = new MainnetControllerInit.CentrifugeRecipient[](2);
        // Note: Re-setting the Avalanche Centrifuge recipient set in the GroveEthereum_20250821 proposal
        centrifugeRecipients[0] = MainnetControllerInit.CentrifugeRecipient({
            destinationCentrifugeId : GroveLiquidityLayerHelpers.AVALANCHE_DESTINATION_CENTRIFUGE_ID,
            recipient               : CastingHelpers.addressToCentrifugeRecipient(Avalanche.ALM_PROXY)
        });
        // Note: Re-setting the Plume Centrifuge recipient set in the GroveEthereum_20251002 proposal
        centrifugeRecipients[1] = MainnetControllerInit.CentrifugeRecipient({
            destinationCentrifugeId : GroveLiquidityLayerHelpers.PLUME_DESTINATION_CENTRIFUGE_ID,
            recipient               : CastingHelpers.addressToCentrifugeRecipient(Plume.ALM_PROXY)
        });

        MainnetControllerInit.upgradeController(
            ControllerInstance({
                almProxy   : Ethereum.ALM_PROXY,
                controller : NEW_CONTROLLER,
                rateLimits : Ethereum.ALM_RATE_LIMITS
            }),
            MainnetControllerInit.ConfigAddressParams({
                freezer       : Ethereum.ALM_FREEZER,
                relayers      : relayers,
                oldController : Ethereum.ALM_CONTROLLER
            }),
            MainnetControllerInit.CheckAddressParams({
                admin                    : Ethereum.GROVE_PROXY,
                proxy                    : Ethereum.ALM_PROXY,
                rateLimits               : Ethereum.ALM_RATE_LIMITS,
                vault                    : Ethereum.ALLOCATOR_VAULT,
                psm                      : Ethereum.PSM,
                daiUsds                  : Ethereum.DAI_USDS,
                cctp                     : Ethereum.CCTP_TOKEN_MESSENGER_V2,
                uniswapV3Router          : Ethereum.UNISWAP_V3_SWAP_ROUTER_02,
                uniswapV3PositionManager : Ethereum.UNISWAP_V3_POSITION_MANAGER
            }),
            mintRecipients,
            new MainnetControllerInit.LayerZeroRecipient[](0),
            centrifugeRecipients
        );

        // Note: Set to make GroveEthereum_20250807 Ethena deposit onboarding backwards compatible
        MainnetController(NEW_CONTROLLER).setMaxExchangeRate(
            Ethereum.SUSDE,
            1e18,
            1.3e18
        );

        // Note: Set to make GroveEthereum_20251211 Morpho vault onboarding backwards compatible
        MainnetController(NEW_CONTROLLER).setMaxExchangeRate(
            Ethereum.GROVE_X_STEAKHOUSE_USDC_MORPHO_VAULT,
            1e18,
            1.15e6
        );

        // Note: Re-setting the Curve RLUSD/USDC pool slippage set in GroveEthereum_20251030
        MainnetController(NEW_CONTROLLER).setMaxSlippage(
            Ethereum.CURVE_RLUSD_USDC,
            0.9990e18
        );

        // Note: Set to make GroveEthereum_20251030 Aave Core RLUSD onboarding backwards compatible
        MainnetController(NEW_CONTROLLER).setMaxSlippage(
            Ethereum.AAVE_CORE_RLUSD,
            0.9990e18
        );

        // Note: Set to make GroveEthereum_20251030 Aave Core USDC onboarding backwards compatible
        MainnetController(NEW_CONTROLLER).setMaxSlippage(
            Ethereum.AAVE_CORE_USDC,
            0.9990e18
        );

        // Note: Set to make GroveEthereum_20251030 Aave Horizon RLUSD onboarding backwards compatible
        MainnetController(NEW_CONTROLLER).setMaxSlippage(
            Ethereum.AAVE_HORIZON_RLUSD,
            0.9990e18
        );

        // Note: Set to make GroveEthereum_20251030 Aave Horizon USDC onboarding backwards compatible
        MainnetController(NEW_CONTROLLER).setMaxSlippage(
            Ethereum.AAVE_HORIZON_USDC,
            0.9990e18
        );
    }

    function _onboardCctpTransfersToBase() internal {
        MainnetController(NEW_CONTROLLER).setMintRecipient(
            CCTPv2Forwarder.DOMAIN_ID_CIRCLE_BASE,
            CastingHelpers.addressToCctpRecipient(Base.ALM_PROXY)
        );

        // Note: General key rate limit for all CCTP transfers was set in the GroveEthereum_20250807 proposal
        bytes32 domainKey = RateLimitHelpers.makeDomainKey(
            MainnetController(NEW_CONTROLLER).LIMIT_USDC_TO_DOMAIN(),
            CCTPv2Forwarder.DOMAIN_ID_CIRCLE_BASE
        );
        IRateLimits(Ethereum.ALM_RATE_LIMITS).setRateLimitData(domainKey, CCTP_RATE_LIMIT_MAX, CCTP_RATE_LIMIT_SLOPE);
    }

}
